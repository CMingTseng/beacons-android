apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-internal.pro', 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'

            task ('generateReleaseJavadoc', type: Javadoc) {
                title 'UriIO Android library reference'
                description 'Library documentation'
                source sourceSets.main.java.srcDirs
                classpath = files(android.getBootClasspath())
                options {
                    links "http://docs.oracle.com/javase/7/docs/api/"
                    linksOffline "http://d.android.com/reference/", "${android.sdkDirectory}/docs/reference"
                }
                include '**/com/uriio/beacons/**'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'org.whispersystems:curve25519-android:0.2.5'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.github.dcendents.android-maven'

task javadocsJar(type: Jar, dependsOn: generateReleaseJavadoc) {
    classifier = 'javadoc'
    from generateReleaseJavadoc.destinationDir
    // options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'UriIO Android BLE beacons advertising library'
            groupId 'com.uriio'
            artifactId 'beacons-android'
            version PUBLISH_VERSION
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocsJar
}